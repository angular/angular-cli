# Copyright Google Inc. All Rights Reserved.
#
# Use of this source code is governed by an MIT-style license that can be
# found in the LICENSE file at https://angular.dev/license

load("@npm//@angular/build-tooling/bazel/api-golden:index.bzl", "api_golden_test_npm_package")
load("@npm//@bazel/jasmine:index.bzl", "jasmine_node_test")
load("//tools:defaults.bzl", "pkg_npm")
load("//tools:interop.bzl", "ts_project")
load("//tools:ts_json_schema.bzl", "ts_json_schema")

licenses(["notice"])

package(default_visibility = ["//visibility:public"])

ts_json_schema(
    name = "app_shell_schema",
    src = "src/builders/app-shell/schema.json",
)

ts_json_schema(
    name = "browser_schema",
    src = "src/builders/browser/schema.json",
)

ts_json_schema(
    name = "browser_esbuild_schema",
    src = "src/builders/browser-esbuild/schema.json",
)

ts_json_schema(
    name = "dev_server_schema",
    src = "src/builders/dev-server/schema.json",
)

ts_json_schema(
    name = "extract_i18n_schema",
    src = "src/builders/extract-i18n/schema.json",
)

ts_json_schema(
    name = "jest_schema",
    src = "src/builders/jest/schema.json",
)

ts_json_schema(
    name = "karma_schema",
    src = "src/builders/karma/schema.json",
)

ts_json_schema(
    name = "protractor_schema",
    src = "src/builders/protractor/schema.json",
)

ts_json_schema(
    name = "server_schema",
    src = "src/builders/server/schema.json",
)

ts_json_schema(
    name = "ng_packagr_schema",
    src = "src/builders/ng-packagr/schema.json",
)

ts_json_schema(
    name = "ssr_dev_server_schema",
    src = "src/builders/ssr-dev-server/schema.json",
)

ts_json_schema(
    name = "prerender_schema",
    src = "src/builders/prerender/schema.json",
)

ts_json_schema(
    name = "web_test_runner_schema",
    src = "src/builders/web-test-runner/schema.json",
)

ts_project(
    name = "build_angular",
    srcs = glob(
        include = [
            "src/**/*.ts",
            "plugins/**/*.ts",
        ],
        exclude = [
            "src/test-utils.ts",
            "src/**/*_spec.ts",
            "src/**/tests/**/*.ts",
            "plugins/**/*_spec.ts",
            "src/testing/**/*.ts",
        ],
    ) + [
        "index.ts",
        "//packages/angular_devkit/build_angular:src/builders/app-shell/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/browser-esbuild/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/browser/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/dev-server/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/extract-i18n/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/jest/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/karma/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/ng-packagr/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/prerender/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/protractor/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/server/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/ssr-dev-server/schema.ts",
        "//packages/angular_devkit/build_angular:src/builders/web-test-runner/schema.ts",
    ],
    data = glob(
        include = [
            "src/**/schema.json",
            "src/**/*.js",
            "src/**/*.mjs",
            "src/**/*.html",
        ],
    ) + [
        "builders.json",
        "package.json",
    ],
    module_name = "@angular-devkit/build-angular",
    deps = [
        "//:root_modules/@ampproject/remapping",
        "//:root_modules/@angular/common",
        "//:root_modules/@angular/compiler-cli",
        "//:root_modules/@angular/core",
        "//:root_modules/@angular/localize",
        "//:root_modules/@angular/platform-server",
        "//:root_modules/@angular/service-worker",
        "//:root_modules/@babel/core",
        "//:root_modules/@babel/generator",
        "//:root_modules/@babel/helper-annotate-as-pure",
        "//:root_modules/@babel/helper-split-export-declaration",
        "//:root_modules/@babel/plugin-transform-async-generator-functions",
        "//:root_modules/@babel/plugin-transform-async-to-generator",
        "//:root_modules/@babel/plugin-transform-runtime",
        "//:root_modules/@babel/preset-env",
        "//:root_modules/@babel/runtime",
        "//:root_modules/@discoveryjs/json-ext",
        "//:root_modules/@types/babel__core",
        "//:root_modules/@types/babel__generator",
        "//:root_modules/@types/browser-sync",
        "//:root_modules/@types/karma",
        "//:root_modules/@types/less",
        "//:root_modules/@types/loader-utils",
        "//:root_modules/@types/node",
        "//:root_modules/@types/picomatch",
        "//:root_modules/@types/semver",
        "//:root_modules/@types/watchpack",
        "//:root_modules/@vitejs/plugin-basic-ssl",
        "//:root_modules/@web/test-runner",
        "//:root_modules/ajv",
        "//:root_modules/ansi-colors",
        "//:root_modules/autoprefixer",
        "//:root_modules/babel-loader",
        "//:root_modules/browserslist",
        "//:root_modules/copy-webpack-plugin",
        "//:root_modules/css-loader",
        "//:root_modules/esbuild",
        "//:root_modules/esbuild-wasm",
        "//:root_modules/fast-glob",
        "//:root_modules/http-proxy-middleware",
        "//:root_modules/istanbul-lib-instrument",
        "//:root_modules/jsonc-parser",
        "//:root_modules/karma",
        "//:root_modules/karma-source-map-support",
        "//:root_modules/less",
        "//:root_modules/less-loader",
        "//:root_modules/license-webpack-plugin",
        "//:root_modules/loader-utils",
        "//:root_modules/mini-css-extract-plugin",
        "//:root_modules/ng-packagr",
        "//:root_modules/open",
        "//:root_modules/ora",
        "//:root_modules/piscina",
        "//:root_modules/postcss",
        "//:root_modules/postcss-loader",
        "//:root_modules/resolve-url-loader",
        "//:root_modules/rxjs",
        "//:root_modules/sass",
        "//:root_modules/sass-loader",
        "//:root_modules/semver",
        "//:root_modules/source-map-loader",
        "//:root_modules/source-map-support",
        "//:root_modules/terser",
        "//:root_modules/tree-kill",
        "//:root_modules/tslib",
        "//:root_modules/typescript",
        "//:root_modules/webpack",
        "//:root_modules/webpack-dev-middleware",
        "//:root_modules/webpack-dev-server",
        "//:root_modules/webpack-merge",
        "//:root_modules/webpack-subresource-integrity",
        "//packages/angular/build:build_rjs",
        "//packages/angular/build/private:private_rjs",
        "//packages/angular/ssr:ssr_rjs",
        "//packages/angular_devkit/architect",
        "//packages/angular_devkit/build_webpack:build_webpack_rjs",
        "//packages/angular_devkit/core:core_rjs",
        "//packages/angular_devkit/core/node:node_rjs",
        "//packages/ngtools/webpack:webpack_rjs",
    ],
)

ts_project(
    name = "build_angular_test_lib",
    testonly = True,
    srcs = glob(
        include = [
            "src/**/*_spec.ts",
        ],
        exclude = [
            "src/builders/**/*_spec.ts",
        ],
    ),
    data = glob(["test/**/*"]),
    deps = [
        ":build_angular_rjs",
        ":build_angular_test_utils_rjs",
        "//:root_modules/fast-glob",
        "//:root_modules/prettier",
        "//:root_modules/typescript",
        "//:root_modules/webpack",
        "//packages/angular_devkit/architect/testing:testing_rjs",
        "//packages/angular_devkit/core:core_rjs",
    ],
)

jasmine_node_test(
    name = "build_angular_test",
    srcs = [":build_angular_test_lib"],
)

genrule(
    name = "license",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = "cp $(execpath //:LICENSE) $@",
)

pkg_npm(
    name = "npm_package",
    pkg_deps = [
        "//packages/angular/build:package.json",
        "//packages/angular_devkit/architect:package.json",
        "//packages/angular_devkit/build_webpack:package.json",
        "//packages/angular_devkit/core:package.json",
        "//packages/ngtools/webpack:package.json",
    ],
    tags = ["release-package"],
    deps = [
        ":README.md",
        ":build_angular",
        ":license",
    ],
)

api_golden_test_npm_package(
    name = "build_angular_api",
    data = [
        ":npm_package",
        "//goldens:public-api",
    ],
    golden_dir = "angular_cli/goldens/public-api/angular_devkit/build_angular",
    npm_package = "angular_cli/packages/angular_devkit/build_angular/npm_package",
)

# Large build_angular specs

ts_project(
    name = "build_angular_test_utils",
    testonly = True,
    srcs = glob(
        include = [
            "src/testing/**/*.ts",
            "src/**/tests/*.ts",
        ],
        exclude = [
            "src/**/*_spec.ts",
        ],
    ),
    data = glob(["test/**/*"]),
    deps = [
        ":build_angular_rjs",
        "//:root_modules/@types/jasmine",
        "//modules/testing/builder:builder_rjs",
        "//packages/angular/build:build_rjs",
        "//packages/angular/build/private:private_rjs",
        "//packages/angular_devkit/architect:architect_rjs",
        "//packages/angular_devkit/architect/node:node_rjs",
        "//packages/angular_devkit/architect/testing:testing_rjs",
        "//packages/angular_devkit/core:core_rjs",
        "//packages/angular_devkit/core/node:node_rjs",
        "@npm//rxjs",
    ],
)

LARGE_SPECS = {
    "app-shell": {},
    "dev-server": {
        "shards": 10,
        "size": "large",
        "flaky": True,
        "extra_deps": [
            "//packages/angular_devkit/build_webpack:build_webpack_rjs",
            "//:root_modules/@types/http-proxy",
            "//:root_modules/http-proxy",
            "//:root_modules/puppeteer",
            "//:root_modules/undici",
        ],
    },
    "extract-i18n": {},
    "karma": {
        "shards": 6,
        "size": "large",
        "flaky": True,
        "extra_deps": [
            "//:root_modules/karma",
            "//:root_modules/karma-chrome-launcher",
            "//:root_modules/karma-coverage",
            "//:root_modules/karma-jasmine",
            "//:root_modules/karma-jasmine-html-reporter",
            "//:root_modules/puppeteer",
            "//:root_modules/webpack",
        ],
    },
    "protractor": {
        "extra_deps": [
            "//:root_modules/jasmine-spec-reporter",
            "//:root_modules/protractor",
            "//:root_modules/puppeteer",
            "//:root_modules/ts-node",
        ],
        # NB: does not run on rbe because webdriver manager uses an absolute path to chromedriver
        "tags": ["no-remote-exec"],
        # NB: multiple shards will compete for port 4200 so limiting to 1
        "shards": 1,
    },
    "server": {
        "size": "large",
        "extra_deps": [
            "//:root_modules/@angular/animations",
        ],
    },
    "ng-packagr": {},
    "browser": {
        "shards": 10,
        "size": "large",
        "flaky": True,
        "extra_deps": [
            "//:root_modules/@angular/animations",
            "//:root_modules/@angular/material",
        ],
    },
    "prerender": {},
    "browser-esbuild": {},
    "ssr-dev-server": {
        "extra_deps": [
            "//packages/angular/ssr/node:node_rjs",
            "//:root_modules/@types/browser-sync",
            "//:root_modules/browser-sync",
            "//:root_modules/express",
            "//:root_modules/undici",
        ],
    },
}

[
    ts_project(
        name = "build_angular_" + spec + "_test_lib",
        testonly = True,
        srcs = glob(["src/builders/" + spec + "/**/*_spec.ts"]),
        deps = [
            # Dependencies needed to compile and run the specs themselves.
            ":build_angular_rjs",
            "//packages/angular_devkit/core:core_rjs",
            "//packages/angular_devkit/core/node:node_rjs",
            "//modules/testing/builder:builder_rjs",
            ":build_angular_test_utils_rjs",
            "//packages/angular/build:build_rjs",
            "//packages/angular/build/private:private_rjs",
            "//packages/angular_devkit/architect:architect_rjs",
            "//packages/angular_devkit/architect/node:node_rjs",
            "//packages/angular_devkit/architect/testing:testing_rjs",

            # Base dependencies for the application in hello-world-app.
            # Some tests also require extra dependencies.
            "//:root_modules/@angular/common",
            "//:root_modules/@angular/compiler",
            "//:root_modules/@angular/compiler-cli",
            "//:root_modules/@angular/core",
            "//:root_modules/@angular/platform-browser",
            "//:root_modules/@angular/platform-browser-dynamic",
            "//:root_modules/@angular/router",
            "//:root_modules/rxjs",
            "//:root_modules/tslib",
            "//:root_modules/typescript",
            "//:root_modules/zone.js",
            "//:root_modules/@types/jasmine",
        ] + LARGE_SPECS[spec].get("extra_deps", []),
    )
    for spec in LARGE_SPECS
]

[
    jasmine_node_test(
        name = "build_angular_" + spec + "_test",
        size = LARGE_SPECS[spec].get("size", "medium"),
        flaky = LARGE_SPECS[spec].get("flaky", False),
        shard_count = LARGE_SPECS[spec].get("shards", 2),
        # These tests are resource intensive and should not be over-parallized as they will
        # compete for the resources of other parallel tests slowing everything down.
        # Ask Bazel to allocate multiple CPUs for these tests with "cpu:n" tag.
        tags = [
            "cpu:2",
        ] + LARGE_SPECS[spec].get("tags", []),
        deps = [":build_angular_" + spec + "_test_lib"],
    )
    for spec in LARGE_SPECS
]
