# Copyright Google Inc. All Rights Reserved.
#
# Use of this source code is governed by an MIT-style license that can be
# found in the LICENSE file at https://angular.io/license

load("@npm//@bazel/jasmine:index.bzl", "jasmine_node_test")
load("//tools:defaults.bzl", "pkg_npm", "ts_library")
load("//tools:toolchain_info.bzl", "TOOLCHAINS_NAMES", "TOOLCHAINS_VERSIONS")

licenses(["notice"])

package(default_visibility = ["//visibility:public"])

ts_library(
    name = "benchmark",
    package_name = "@angular-devkit/benchmark",
    srcs = glob(
        include = ["src/**/*.ts"],
        exclude = [
            "src/**/*_spec.ts",
            "src/**/*_benchmark.ts",
        ],
    ),
    module_name = "@angular-devkit/benchmark",
    module_root = "src/index.d.ts",
    deps = [
        "//packages/angular_devkit/core",
        "//packages/angular_devkit/core/node",
        "@npm//@types/node",
        "@npm//@types/pidusage",
        "@npm//@types/yargs-parser",
        "@npm//ansi-colors",
        "@npm//rxjs",
    ],
)

ts_library(
    name = "benchmark_test_lib",
    testonly = True,
    srcs = glob(["src/**/*_spec.ts"]),
    data = [
        "src/test/exit-code-one.js",
        "src/test/fibonacci.js",
        "src/test/test-script.js",
        "src/test/watch-test-cmd.js",
        "src/test/watch-test-file.txt",
        "src/test/watch-test-script.js",
    ],
    # @external_begin
    deps = [
        ":benchmark",
        "//packages/angular_devkit/core",
        "//packages/angular_devkit/core/node",
        "@npm//rxjs",
    ],
    # @external_end
)

[
    jasmine_node_test(
        name = "benchmark_test_" + toolchain_name,
        srcs = [":benchmark_test_lib"],
        tags = [toolchain_name],
        toolchain = toolchain,
        deps = [
            "@npm//jasmine",
            "@npm//pidtree",
            "@npm//pidusage",
            "@npm//source-map",
            "@npm//tree-kill",
            "@npm//yargs-parser",
        ],
    )
    for toolchain_name, toolchain in zip(
        TOOLCHAINS_NAMES,
        TOOLCHAINS_VERSIONS,
    )
]

genrule(
    name = "license",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = "cp $(execpath //:LICENSE) $@",
)

pkg_npm(
    name = "npm_package",
    pkg_deps = [
        "//packages/angular_devkit/core:package.json",
    ],
    deps = [
        "src/test/exit-code-one.js",
        "src/test/fibonacci.js",
        "src/test/test-script.js",
        "src/test/watch-test-cmd.js",
        "src/test/watch-test-file.txt",
        "src/test/watch-test-script.js",
        ":README.md",
        ":benchmark",
        ":license",
    ],
)
