load("@npm//@angular/build-tooling/bazel/api-golden:index.bzl", "api_golden_test_npm_package")
load("@npm//@bazel/jasmine:index.bzl", "jasmine_node_test")
load("//tools:defaults.bzl", "pkg_npm")
load("//tools:interop.bzl", "ts_project")
load("//tools:ts_json_schema.bzl", "ts_json_schema")

licenses(["notice"])

package(default_visibility = ["//visibility:public"])

ts_json_schema(
    name = "application_schema",
    src = "src/builders/application/schema.json",
)

ts_json_schema(
    name = "dev-server_schema",
    src = "src/builders/dev-server/schema.json",
)

ts_json_schema(
    name = "extract_i18n_schema",
    src = "src/builders/extract-i18n/schema.json",
)

ts_project(
    name = "build",
    srcs = glob(
        include = [
            "src/**/*.ts",
        ],
        exclude = [
            "src/test-utils.ts",
            "src/**/*_spec.ts",
            "src/**/tests/**/*.ts",
            "src/testing/**/*.ts",
        ],
    ) + [
        "index.ts",
        "//packages/angular/build:src/builders/application/schema.ts",
        "//packages/angular/build:src/builders/dev-server/schema.ts",
        "//packages/angular/build:src/builders/extract-i18n/schema.ts",
    ],
    data = glob(
        include = [
            "src/**/schema.json",
            "src/**/*.js",
            "src/**/*.mjs",
            "src/**/*.html",
        ],
    ) + [
        "builders.json",
        "package.json",
    ],
    module_name = "@angular/build",
    deps = [
        "//:root_modules/@ampproject/remapping",
        "//:root_modules/@angular/common",
        "//:root_modules/@angular/compiler",
        "//:root_modules/@angular/compiler-cli",
        "//:root_modules/@angular/core",
        "//:root_modules/@angular/localize",
        "//:root_modules/@angular/platform-server",
        "//:root_modules/@angular/service-worker",
        "//:root_modules/@babel/core",
        "//:root_modules/@babel/helper-annotate-as-pure",
        "//:root_modules/@babel/helper-split-export-declaration",
        "//:root_modules/@babel/plugin-syntax-import-attributes",
        "//:root_modules/@inquirer/confirm",
        "//:root_modules/@types/babel__core",
        "//:root_modules/@types/less",
        "//:root_modules/@types/node",
        "//:root_modules/@types/picomatch",
        "//:root_modules/@types/semver",
        "//:root_modules/@types/watchpack",
        "//:root_modules/@vitejs/plugin-basic-ssl",
        "//:root_modules/beasties",
        "//:root_modules/browserslist",
        "//:root_modules/esbuild",
        "//:root_modules/esbuild-wasm",
        "//:root_modules/fast-glob",
        "//:root_modules/https-proxy-agent",
        "//:root_modules/jsonc-parser",
        "//:root_modules/listr2",
        "//:root_modules/lmdb",
        "//:root_modules/magic-string",
        "//:root_modules/mrmime",
        "//:root_modules/parse5-html-rewriting-stream",
        "//:root_modules/picomatch",
        "//:root_modules/piscina",
        "//:root_modules/postcss",
        "//:root_modules/rollup",
        "//:root_modules/sass",
        "//:root_modules/semver",
        "//:root_modules/tslib",
        "//:root_modules/typescript",
        "//:root_modules/vite",
        "//:root_modules/watchpack",
        "//packages/angular/ssr:ssr_rjs",
        "//packages/angular/ssr/node:node_rjs",
        "//packages/angular_devkit/architect:architect_rjs",
    ],
)

ts_project(
    name = "unit_test_lib",
    testonly = True,
    srcs = glob(
        include = ["src/**/*_spec.ts"],
        exclude = ["src/builders/**/tests/**"],
    ),
    deps = [
        ":build_rjs",
        "//:root_modules/@angular/compiler-cli",
        "//:root_modules/@babel/core",
        "//:root_modules/@types/jasmine",
        "//:root_modules/prettier",
        "//:root_modules/typescript",
        "//packages/angular/build/private:private_rjs",
        "//packages/angular_devkit/core:core_rjs",
        "//packages/angular_devkit/core/node:node_rjs",
    ],
)

jasmine_node_test(
    name = "unit_tests",
    deps = [":unit_test_lib"],
)

ts_project(
    name = "application_integration_test_lib",
    testonly = True,
    srcs = glob(include = ["src/builders/application/tests/**/*.ts"]),
    deps = [
        ":build_rjs",
        "//packages/angular/build/private:private_rjs",
        "//modules/testing/builder:builder_rjs",
        "//packages/angular_devkit/architect:architect_rjs",
        "//packages/angular_devkit/architect/node:node_rjs",
        "//packages/angular_devkit/architect/testing:testing_rjs",
        "//packages/angular_devkit/core:core_rjs",
        "//packages/angular_devkit/core/node:node_rjs",

        # Base dependencies for the application in hello-world-app.
        "//:root_modules/@angular/common",
        "//:root_modules/@angular/compiler",
        "//:root_modules/@angular/compiler-cli",
        "//:root_modules/@angular/core",
        "//:root_modules/@angular/platform-browser",
        "//:root_modules/@angular/platform-browser-dynamic",
        "//:root_modules/@angular/router",
        "//:root_modules/rxjs",
        "//:root_modules/tslib",
        "//:root_modules/typescript",
        "//:root_modules/zone.js",
        "//:root_modules/buffer",
    ],
)

ts_project(
    name = "dev-server_integration_test_lib",
    testonly = True,
    srcs = glob(include = ["src/builders/dev-server/tests/**/*.ts"]),
    deps = [
        ":build_rjs",
        "//packages/angular/build/private:private_rjs",
        "//modules/testing/builder:builder_rjs",
        "//packages/angular_devkit/architect:architect_rjs",
        "//packages/angular_devkit/architect/node:node_rjs",
        "//packages/angular_devkit/architect/testing:testing_rjs",
        "//packages/angular_devkit/core:core_rjs",
        "//packages/angular_devkit/core/node:node_rjs",

        # dev server only test deps
        "//:root_modules/@types/http-proxy",
        "//:root_modules/http-proxy",
        "//:root_modules/puppeteer",

        # Base dependencies for the application in hello-world-app.
        "//:root_modules/@angular/common",
        "//:root_modules/@angular/compiler",
        "//:root_modules/@angular/compiler-cli",
        "//:root_modules/@angular/core",
        "//:root_modules/@angular/platform-browser",
        "//:root_modules/@angular/platform-browser-dynamic",
        "//:root_modules/@angular/router",
        "//:root_modules/rxjs",
        "//:root_modules/tslib",
        "//:root_modules/typescript",
        "//:root_modules/zone.js",
        "//:root_modules/buffer",
    ],
)

jasmine_node_test(
    name = "application_integration_tests",
    size = "large",
    flaky = True,
    shard_count = 10,
    deps = [":application_integration_test_lib"],
)

jasmine_node_test(
    name = "dev-server_integration_tests",
    size = "large",
    flaky = True,
    shard_count = 10,
    deps = [":dev-server_integration_test_lib"],
)

genrule(
    name = "license",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = "cp $(execpath //:LICENSE) $@",
)

pkg_npm(
    name = "npm_package",
    pkg_deps = [
        "//packages/angular_devkit/architect:package.json",
    ],
    tags = ["release-package"],
    deps = [
        ":README.md",
        ":build",
        ":license",
        "//packages/angular/build/private",
    ],
)

api_golden_test_npm_package(
    name = "api",
    data = [
        ":npm_package",
        "//goldens:public-api",
    ],
    golden_dir = "angular_cli/goldens/public-api/angular/build",
    npm_package = "angular_cli/packages/angular/build/npm_package",
)
