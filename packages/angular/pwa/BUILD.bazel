# Copyright Google Inc. All Rights Reserved.
#
# Use of this source code is governed by an MIT-style license that can be
# found in the LICENSE file at https://angular.dev/license

load("@npm//@bazel/jasmine:index.bzl", "jasmine_node_test")
load("//tools:defaults2.bzl", "npm_package", "ts_project")
load("//tools:ts_json_schema.bzl", "ts_json_schema")

licenses(["notice"])

package(default_visibility = ["//visibility:public"])

RUNTIME_ASSETS = glob(
    include = [
        "pwa/*.js",
        "pwa/*.mjs",
        "pwa/files/**/*",
    ],
) + [
    "package.json",
    "collection.json",
    "pwa/schema.json",
]

ts_project(
    name = "pwa",
    srcs = [
        "pwa/index.ts",
        "//packages/angular/pwa:pwa/schema.ts",
    ],
    data = RUNTIME_ASSETS,
    module_name = "@angular/pwa",
    deps = [
        "//:root_modules/@types/node",
        "//:root_modules/parse5-html-rewriting-stream",
        "//packages/angular_devkit/schematics:schematics_rjs",
        "//packages/schematics/angular:angular_rjs",
    ],
)

ts_json_schema(
    name = "pwa_schema",
    src = "pwa/schema.json",
)

ts_project(
    name = "pwa_test_lib",
    testonly = True,
    srcs = glob(["pwa/**/*_spec.ts"]),
    deps = [
        ":pwa_rjs",
        "//:root_modules/@types/jasmine",
        "//packages/angular_devkit/schematics/testing:testing_rjs",
    ],
)

jasmine_node_test(
    name = "pwa_test",
    srcs = [":pwa_test_lib"],
)

genrule(
    name = "license",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = "cp $(execpath //:LICENSE) $@",
)

npm_package(
    name = "pkg",
    pkg_deps = [
        "//packages/angular_devkit/schematics:package.json",
        "//packages/schematics/angular:package.json",
    ],
    tags = ["release-package"],
    deps = RUNTIME_ASSETS + [
        ":README.md",
        ":license",
        ":pwa_rjs",
    ],
)
